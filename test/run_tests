#!/usr/bin/perl
use strict;

sub interrupt
{
	exit 25
}

sub basename
{
	my $f = shift;
	return $1 if $f =~ m;/([^/]+)$;;
	return $f;
}

sub usage
{
	die "Usage: $0 [-q | -v] [tests...]\n";
}

sub test
{
	my %args = @_; # keys = file, output, verbose

	my $pid = fork();
	if($pid == 0){
		# ./test.pl -v "$1" < /dev/null > "$2" 2>&1
		open STDIN, '</dev/null' or die;
		open STDOUT, '>', $args{output} or die;
		open STDERR, '>&STDOUT' or die;

		my @args = @args{file};
		unshift @args, '-v' if $args{verbose};
		exec './test.pl', @args;
		die "exec: $!";
	}
	if(wait() != $pid){
		die "bad wait return? $!";
	}

	my $r = $?;
	return $r;
}

sub cat_indent_file
{
	my $f = shift;
	open F, '<', $f or die "open $f: $!\n";
	print "\t$_" while <F>;
	close F;
}

$SIG{INT} = \&interrupt;

my $verbose = 0;
my $quiet = 0;
my @tests;

for(my $i = 0; $i < @ARGV; $i++){
	if($ARGV[$i] eq '-v'){
		$verbose = 1;
	}elsif($ARGV[$i] eq '-q'){
		$quiet = 1;

	}else{
		push @tests, $ARGV[$i];
	}
}

die "can't be quiet and verbose" if $quiet and $verbose;

my $tdir = '/tmp/ucc.test/';
my @unlinks;
push @unlinks, $tdir;
if(not -d $tdir and not mkdir($tdir)){
	die "mkdir: $!";
}
$ENV{UCC_TESTDIR} = $tdir;

my $ec = 0;

sub find_tests
{
	# DFS
	my @found;
	for(my $i = 0; $i < @_; $i++){
		$_ = $_[$i];
		if(-d $_){
			push @found, find_tests(glob "$_/*");
		}elsif($_ =~ /\.c$/){
			push @found, $_;
		}
	}
	return @found;
}

my @test_files = find_tests(@tests);
die "$0: no tests\n" unless @test_files;

for my $f (@test_files){
	chomp($f);

	print "$0: $f\n" if $verbose;

	my %args = (
		file => $f,
		output => "$tdir/" . basename($f),
		verbose => $verbose
	);

	my $rc = test(%args);

	if($rc & 127){
		die "test caught signal " . ($rc & 127) . "\n";
	}
	$rc >>= 8; # exit code

	my $desc;
	my $failed = 0;

	if($rc == 1){
		$desc = 'fail';
		$failed = 1;
	}elsif($rc == 2){
		$desc = 'skip';
	}else{
		$desc = 'pass';
	}

	print "$desc: $f";

	if($failed != 0){
		print "\n";

		$ec = 1;
		# dump output
		cat_indent_file($args{output});
	}elsif($quiet){
		# clear to eol, move cursor to start
		print "\e[K\r";
	}else{
		print "\n";
	}
}

END
{
	my $r = $?;
	system('rm', '-r', @unlinks) if @unlinks;
	$? = $r;
}

exit $ec;
